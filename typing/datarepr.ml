(**************************************************************************)
(*                                                                        *)
(*                                 OCaml                                  *)
(*                                                                        *)
(*             Xavier Leroy, projet Cristal, INRIA Rocquencourt           *)
(*                                                                        *)
(*   Copyright 1996 Institut National de Recherche en Informatique et     *)
(*     en Automatique.                                                    *)
(*                                                                        *)
(*   All rights reserved.  This file is distributed under the terms of    *)
(*   the GNU Lesser General Public License version 2.1, with the          *)
(*   special exception on linking described in the file LICENSE.          *)
(*                                                                        *)
(**************************************************************************)

(* Compute constructor and label descriptions from type declarations,
   determining their representation. *)

open Asttypes
open Types
open Data_types
open Btype

(* TODO: register an error printer for this type. *)
type error =
  | Multiple_args_unboxed_constructor of Ident.t

exception Error of Location.t * error

(* Simplified version of Ctype.free_vars *)
let free_vars ?(param=false) ty =
  let ret = ref TypeSet.empty in
  with_type_mark begin fun mark ->
    let rec loop ty =
      if try_mark_node mark ty then
        match get_desc ty with
        | Tvar _ ->
            ret := TypeSet.add ty !ret
        | Tvariant row ->
            iter_row loop row;
            if not (static_row row) then begin
              match get_desc (row_more row) with
              | Tvar _ when param -> ret := TypeSet.add ty !ret
              | _ -> loop (row_more row)
            end
                (* XXX: What about Tobject ? *)
        | _ ->
            iter_type_expr loop ty
    in
    loop ty
  end;
  !ret

let newgenconstr path tyl = newgenty (Tconstr (path, tyl, ref Mnil))

let constructor_existentials cd_args cd_res =
  let tyl =
    match cd_args with
    | Cstr_tuple l -> l
    | Cstr_record l -> List.map (fun l -> l.ld_type) l
  in
  let existentials =
    match cd_res with
    | None -> []
    | Some type_ret ->
        let arg_vars_set = free_vars (newgenty (Ttuple tyl)) in
        let res_vars = free_vars type_ret in
        TypeSet.elements (TypeSet.diff arg_vars_set res_vars)
  in
  (tyl, existentials)

let constructor_args ~current_unit priv cd_args cd_res path rep =
  let tyl, existentials = constructor_existentials cd_args cd_res in
  match cd_args with
  | Cstr_tuple l -> existentials, l, None
  | Cstr_record lbls ->
      let arg_vars_set = free_vars ~param:true (newgenty (Ttuple tyl)) in
      let type_params = TypeSet.elements arg_vars_set in
      let arity = List.length type_params in
      let tdecl =
        {
          type_params;
          type_arity = arity;
          type_kind = Type_record (lbls, rep);
          type_private = priv;
          type_manifest = None;
          type_variance = Variance.unknown_signature ~injective:true ~arity;
          type_separability = Types.Separability.default_signature ~arity;
          type_is_newtype = false;
          type_expansion_scope = Btype.lowest_level;
          type_loc = Location.none;
          type_attributes = [];
          type_immediate = Unknown;
          type_unboxed_default = false;
          type_uid = Uid.mk ~current_unit;
        }
      in
      existentials,
      [ newgenconstr path type_params ],
      Some tdecl

let constructor_descrs ~current_unit ty_path decl cstrs rep =
  let ty_res = newgenconstr ty_path decl.type_params in
  let variant_unboxed = Builtin_attributes.has_unboxed decl.type_attributes in
  let constructor_descrs =
    (* TODO explain why we tie the knot using a reference here. *)
    ref [] in
  let cstr_type_data =
    let num_consts = ref 0 and num_nonconsts = ref 0 and num_unboxed = ref 0 in
    List.iter
      (fun {cd_args; cd_attributes; _} ->
        if cd_args = Cstr_tuple []
        then incr num_consts
        else if variant_unboxed || Builtin_attributes.has_unboxed cd_attributes
        then incr num_unboxed
        else incr num_nonconsts)
      cstrs;
    Some {
      num_consts = !num_consts;
      num_nonconsts = !num_nonconsts;
      num_unboxed = !num_unboxed;
      repr_data = Misc.Cached.create constructor_descrs;
    }
  in
  let rec describe_constructors idx_const idx_nonconst = function
      [] -> []
    | {cd_id; cd_args; cd_res; cd_loc; cd_attributes; cd_uid} :: rem ->
        let ty_res =
          match cd_res with
          | Some ty_res' -> ty_res'
          | None -> ty_res
        in
        (* A constructor is unboxed if the whole declaration has the
           [@@unboxed] attr, or if it has the [@unboxed] attr *)
        let cstr_is_unboxed = variant_unboxed ||
                              Builtin_attributes.has_unboxed cd_attributes
        in
        let (tag, descr_rem) =
          match cstr_is_unboxed, cd_args with
          | true, Cstr_tuple [ty]
          | true, Cstr_record [{ld_type=ty; _}] ->
              (Cstr_unboxed (Misc.Cached.create ty),
               describe_constructors idx_const idx_nonconst rem)
          | true, _ ->
              raise (Error (cd_loc, Multiple_args_unboxed_constructor cd_id))
          | false, Cstr_tuple [] -> (Cstr_constant idx_const,
                   describe_constructors (idx_const+1) idx_nonconst rem)
          | false, _ -> (Cstr_block idx_nonconst,
                   describe_constructors idx_const (idx_nonconst+1) rem) in
        let cstr_name = Ident.name cd_id in
        let existentials, cstr_args, cstr_inlined =
          let representation =
            match rep with
            | Variant_unboxed -> Record_unboxed true
            | Variant_regular -> Record_inlined idx_nonconst
          in
          constructor_args ~current_unit decl.type_private cd_args cd_res
            Path.(Pextra_ty (ty_path, Pcstr_ty cstr_name)) representation
        in
        let cstr =
          { cstr_name;
            cstr_res = ty_res;
            cstr_existentials = existentials;
            cstr_args;
            cstr_arity = List.length cstr_args;
            cstr_tag = tag;
            cstr_type_data;
            cstr_private = decl.type_private;
            cstr_generalized = cd_res <> None;
            cstr_loc = cd_loc;
            cstr_attributes = cd_attributes;
            cstr_inlined;
            cstr_uid = cd_uid;
          } in
        (cd_id, cstr) :: descr_rem in
  let constructors = describe_constructors 0 0 cstrs in
  constructor_descrs := List.map snd constructors;
  constructors

let repr_data_of_regular_constructors ~get_shape constructors =
  let num_imms = ref 0 in
  let min_imm = ref max_int in
  let max_imm = ref min_int in
  let any_imm = ref false in
  let num_tags = ref 0 in
  let min_tag = ref 255 in
  let max_tag = ref 0 in
  let any_tag = ref false in
  let open Head_shape_types in
  let notify_imm (Imm i) =
    incr num_imms;
    min_imm := min i !min_imm;
    max_imm := max i !max_imm;
  in
  let notify_tag (Tag t) =
    incr num_tags;
    min_tag := min t !min_tag;
    max_tag := max t !max_tag;
  in
  List.iter (fun cstr ->
    match cstr.cstr_tag with
    | Cstr_constant imm ->
        notify_imm (Imm imm)
    | Cstr_block tag ->
        notify_tag (Tag tag)
    | Cstr_extension _ ->
        assert false
    | Cstr_unboxed descr ->
        let shape = get_shape descr in
        let open Head_shape_types in
        begin match shape.imms with
        | Any -> any_imm := true
        | Those imms -> ImmSet.iter notify_imm imms
        end;
        begin match shape.blocks with
        | Any -> any_tag := true
        | Those tag_set ->
            TagSet.iter notify_tag tag_set
        end;
  ) constructors;
  {
    imm_stats =
      if !any_imm then Any
      else Spread { num = !num_imms; min = !min_imm; max = !max_imm };
    tag_stats =
      if !any_tag then Any
      else Spread { num = !num_tags; min = !min_tag; max = !max_tag };
  }

let extension_descr ~current_unit path_ext ext =
  let ty_res =
    match ext.ext_ret_type with
        Some type_ret -> type_ret
      | None -> newgenconstr ext.ext_type_path ext.ext_type_params
  in
  let existentials, cstr_args, cstr_inlined =
    constructor_args ~current_unit ext.ext_private ext.ext_args ext.ext_ret_type
      Path.(Pextra_ty (path_ext, Pext_ty)) (Record_extension path_ext)
  in
    { cstr_name = Path.last path_ext;
      cstr_res = ty_res;
      cstr_existentials = existentials;
      cstr_args;
      cstr_arity = List.length cstr_args;
      cstr_tag = Cstr_extension(path_ext, cstr_args = []);
      cstr_type_data = None;
      cstr_private = ext.ext_private;
      cstr_generalized = ext.ext_ret_type <> None;
      cstr_loc = ext.ext_loc;
      cstr_attributes = ext.ext_attributes;
      cstr_inlined;
      cstr_uid = ext.ext_uid;
    }

let none =
  create_expr (Ttuple []) ~level:(-1) ~scope:Btype.generic_level ~id:(-1)
    (* Clearly ill-formed type *)

let dummy_label =
  { lbl_name = ""; lbl_res = none; lbl_arg = none; lbl_mut = Immutable;
    lbl_pos = (-1); lbl_all = [||]; lbl_repres = Record_regular;
    lbl_private = Public;
    lbl_loc = Location.none;
    lbl_attributes = [];
    lbl_uid = Uid.internal_not_actually_unique;
  }

let label_descrs ty_res lbls repres priv =
  let all_labels = Array.make (List.length lbls) dummy_label in
  let rec describe_labels num = function
      [] -> []
    | l :: rest ->
        let lbl =
          { lbl_name = Ident.name l.ld_id;
            lbl_res = ty_res;
            lbl_arg = l.ld_type;
            lbl_mut = l.ld_mutable;
            lbl_pos = num;
            lbl_all = all_labels;
            lbl_repres = repres;
            lbl_private = priv;
            lbl_loc = l.ld_loc;
            lbl_attributes = l.ld_attributes;
            lbl_uid = l.ld_uid;
          } in
        all_labels.(num) <- lbl;
        (l.ld_id, lbl) :: describe_labels (num+1) rest in
  describe_labels 0 lbls

exception Constr_not_found

let rec find_constr tag num_const num_nonconst = function
  | [] ->
      raise Constr_not_found
  | {cd_args = Cstr_tuple []; _} as c  :: rem ->
      if tag = Cstr_constant num_const
      then c
      else find_constr tag (num_const + 1) num_nonconst rem
  | c :: rem ->
      if tag = Cstr_block num_nonconst
      then c
      else find_constr tag num_const (num_nonconst + 1) rem

let find_constr_by_tag tag cstrlist =
  find_constr tag 0 0 cstrlist

let constructors_of_type ~current_unit ty_path decl =
  match decl.type_kind with
  | Type_variant (cstrs,rep) ->
     constructor_descrs ~current_unit ty_path decl cstrs rep
  | Type_record _ | Type_abstract _ | Type_open -> []

let labels_of_type ty_path decl =
  match decl.type_kind with
  | Type_record(labels, rep) ->
      label_descrs (newgenconstr ty_path decl.type_params)
        labels rep decl.type_private
  | Type_variant _ | Type_abstract _ | Type_open -> []
